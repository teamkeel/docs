import { Callout } from "nextra/components";

# Creating data

## Create products
To populate that list of products, we need a way to create new records. Adding a `create` action will generate a page with a form.
```keel {10,14}
model Product {
    fields {
        name Text
        description Text?
        imageUrl Text?
        ...
    }
    actions {
        ...
        create createProduct() with (name, description?, imageUrl?)
    }
    @permission(
        expression: ctx.isAuthenticated,
        actions: [..., create]
    )
}
```

![Product created](/internal-tools-tutorial-create-product.gif)

This will also create a shortcut linking from your `List products` tool.

![Create product tool](/internal-tools-tutorial-create-product-link.png)

### Nested records
When we create a product, we might want to add new stock items using the same form. We can use [dot notation on action inputs](/actions#creating-nested-records) to do this.

```keel {8}
model Product {
    fields {
        ...
        stock StockItem[]
    }
    actions {
        ...
        create createProduct() with (..., stock.quantity, stock.location.id)
    }
}
```

![Form with nested inputs](/internal-tools-tutorial-create-nested.png)

### Looking up records
We likely won't know the `id` of the `stock.location` record when filling out the form. To help with that, we can create an action that lets you [look it up](/internal-tools/lookups) instead. Since the `location` field relates to `StockLocation`, we can just add a `list` action to `StockLocation`.

```keel {8,10-13}
model StockItem {
    fields {
        location StockLocation
    }
}
model StockLocation {
    actions {
        list listStockLocations()
    }
    @permission(
        expression: ctx.isAuthenticated,
        actions: [list]
    )
}
```

![Lookup fields](/internal-tools-tutorial-lookups.gif)

## Create stock locations
You will probably create stock locations independently from creating products, e.g. when you are setting up your warehouse. Let's generate that tool too in the same way as we did for a product.
```keel {7,11}
model StockLocation {
    fields {
        name @unique
    }
    actions {
        ...
        create createStockLocation() with (name)
    }
    @permission(
        expression: ctx.isAuthenticated,
        actions: [..., create]
    )
}
```

## Create stock items
When you want to re-stock an existing product, you want to create a new stock item and relate it to a product and a location.
```keel {7-13}
model StockItem {
    fields {
        quantity Number
        product Product
        location StockLocation
    }
    actions {
        create createStockItem() with (quantity, product.id, location.id)
    }
    @permission(
        expression: ctx.isAuthenticated,
        actions: [create]
    )
}
```

## Schema

```keel filename="schema.keel" {10,14,24-30,38-45}
model Product {
    fields {
        name Text
        description Text?
        imageUrl Text?
        stock StockItem[]
    }
    actions {
        list listProducts()
        create createProduct() with (name, description?, imageUrl?, stock.quantity, stock.location.id)
    }
    @permission(
        expression: ctx.isAuthenticated,
        actions: [list, create]
    )
}

model StockItem {
    fields {
        product Product
        quantity Number
        location StockLocation
    }
    actions {
        create createStockItem() with (quantity, product.id, location.id)
    }
    @permission(
        expression: ctx.isAuthenticated,
        actions: [create]
    )
}

model StockLocation {
    fields {
        name Text @unique
        items StockItem[]
    }
    actions {
        list listStockLocations()
        create createStockLocation() with (name)
    }
    @permission(
        expression: ctx.isAuthenticated,
        actions: [list, create]
    )
}
```

<Callout emoji="ðŸ‘·â€â™‚ï¸">
    Hey, go add a few stock locations & products to the database!
</Callout>